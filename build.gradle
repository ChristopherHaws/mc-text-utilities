plugins {
	// https://fabricmc.net/wiki/documentation:fabric_loom
	id 'fabric-loom' version '1.7-SNAPSHOT'
    // https://plugins.gradle.org/plugin/io.github.juuxel.loom-vineflower
    //id 'io.github.juuxel.loom-vineflower' version '1.11.0'
	id 'maven-publish'
	id 'idea'
}

String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
Boolean appendBuildNumber = System.getenv("APPEND_BUILD_NUMBER") == "true" && buildNumber != null
String modVersion = System.getenv("MOD_VERSION") ?: "0.0.1"

group = project.maven_group
version = modVersion +
        (appendBuildNumber ? "-build${buildNumber}" : "") +
        "+${project.minecraft_version}"

base {
	archivesName = "${project.archives_base_name}-fabric"
}

repositories {
    mavenCentral()
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.fabricmc.net/" }
	// Needed by modmenu
	// https://placeholders.pb4.eu/dev/getting-started/#__tabbed_1_1
	maven {
        url "https://maven.nucleoid.xyz/"
        name "Nucleoid"
    }

	exclusiveContent {
		forRepository {
			maven {
				name = "CurseForge"
				url = "https://cursemaven.com"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}

    // https://docs.modrinth.com/docs/tutorials/maven/#advanced-repository-declaration-recommended
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"textutilities" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	// https://github.com/SpongePowered/Mixin/wiki/Mixin-Java-System-Properties
	def applyCommonRunConfig = { run ->
        // run.runDir = "run/${project.minecraft_version}/${run.name}"
        // run.vmArg('-XX:+ShowCodeDetailsInExceptionMessages')
		run.property("mixin.debug.export", "true")
		run.property("mixin.debug.export.decompile", "true")
		run.property("mixin.dumpTargetOnFailure", "true")
		run.property("mixin.debug.verify", "true")
		run.property("mixin.debug.verbose", "true")
		run.property("mixin.env.remapRefMap", "true")
		run.property("mixin.checks", "true")
		run.property("mixin.hotSwap", "true")
    }

	runs {
		client {
			runDir = "run/${project.minecraft_version}/client"
			vmArg "-XX:+ShowCodeDetailsInExceptionMessages"
			applyCommonRunConfig(client)
		}
		server {
			runDir = "run/${project.minecraft_version}/server"
			vmArg "-XX:+ShowCodeDetailsInExceptionMessages"
			applyCommonRunConfig(server)
		}
	}
}

sourceSets {
	test {
		compileClasspath += sourceSets.client.compileClasspath
		runtimeClasspath += sourceSets.client.runtimeClasspath
	}
}


dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Mod Compatibility (optional dependencies)
	modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Unit Tests
	testImplementation(platform('org.junit:junit-bom:5.9.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation(sourceSets.client.output)

	// Mods that are useful for local development
	//devMods(dependencies)
}

// mods that can be uncommented to test in development
static def devMods(DependencyHandler deps) {
    deps.modLocalRuntime("maven.modrinth:lazydfu:0.1.3")
    deps.modLocalRuntime("maven.modrinth:lithium:mc1.21-0.12.7")
    deps.modLocalRuntime("maven.modrinth:sodium:mc1.20.4-0.5.8")
    deps.modLocalRuntime("maven.modrinth:starlight:1.1.3+1.20.4")
	deps.modLocalRuntime("maven.modrinth:architectury-api:11.1.17+fabric")
    deps.modLocalRuntime("maven.modrinth:auth-me:8.0.0+1.20.4")
    deps.modLocalRuntime("maven.modrinth:expanded-storage:12.1.0-beta.4+fabric")
}

processResources {
    Map<String, Object> properties = new HashMap<>()
    properties.put("version", project.version)
    properties.put("mod_id", project.mod_id)
    properties.put("mod_name", project.mod_name)
    properties.put("loader_version", project.loader_version)
    properties.put("fabric_version", project.fabric_version)
    properties.put("minecraft_version", project.minecraft_version)
    properties.put("java_version", sourceCompatibility)

	properties.each { k, v -> inputs.property(k, v) }

    filesMatching("fabric.mod.json") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

// configure the maven publication
publishing {
	publications {
		//noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
			//noinspection GroovyAssignabilityCheck
            from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
